IF
	StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_VOCALIZE)
END

IF
	Allegiance(Myself,ENEMY)
	Global("cowledwizard","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_SPELL_DEFLECTION)
		Wait(1)
		DisplayString(Myself,31247) // Tattoo of Power - Spell Immunity
		ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_INVOCATION)
		SetGlobal("cowledwizard","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	Global("cowledwizard","LOCALS",1)
	CheckStatLT(Myself,1,STONESKINS)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,29736) // Contingency - Stoneskin
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		SetGlobal("cowledwizard","LOCALS",2)
END

IF
	See(NearestEnemyOfType([0.0.0.LONG_BOW]))
	HaveSpell(WIZARD_PIERCE_MAGIC)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_PIERCE_MAGIC)
END

IF
	See(NearestEnemyOfType([0.0.0.LONG_BOW]))
	!StateCheck([0.0.0.MAGE],STATE_SILENCED)
	HaveSpell(WIZARD_POWER_WORD_SILENCE)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_SILENCE)
END

IF
	Allegiance(Myself,ENEMY)
	See(SecondNearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	HaveSpell(WIZARD_POWER_WORD_STUN)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END

IF
	Allegiance(Myself,ENEMY)
	HaveSpell(WIZARD_STONE_SKIN)
	CheckStatLT(Myself,1,STONESKINS)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_STONE_SKIN)
END

IF
	See(ThirdNearestEnemyOf(Myself))
	HaveSpell(WIZARD_DISPEL_MAGIC)
	!Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

IF
	See(NearestEnemyOf(Myself))
	DifficultyGT(EASY)
	HaveSpell(WIZARD_HOLD_PERSON)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_HOLD_PERSON)
END

IF
	Allegiance(Myself,ENEMY)
	!Range(ThirdNearestEnemyOf(Myself),10)
	HaveSpell(WIZARD_FIREBALL)
THEN
	RESPONSE #100
		Spell(ThirdNearestEnemyOf(Myself),WIZARD_FIREBALL)
END

IF
	See(SecondNearestEnemyOf(Myself))
	HaveSpell(WIZARD_GREATER_MALISON)
THEN
	RESPONSE #100
		DisplayString(Myself,31024) // Magic Tattoo - Mirror Image
		Spell(SecondNearestEnemyOf(Myself),WIZARD_GREATER_MALISON)
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
END

IF
	Allegiance(Myself,ENEMY)
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
	HaveSpell(WIZARD_DEATH_SPELL)
THEN
	RESPONSE #100
		DisplayString(Myself,31024) // Magic Tattoo - Mirror Image
		Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_LIGHTNING_BOLT)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_LIGHTNING_BOLT)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

IF
	See(NearestEnemyOf(Myself))
	GlobalLT("Spell1","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpellRES("ICEEXPL",NearestEnemyOf(Myself))        //WIZARD_ICE_EXPLODER
		IncrementGlobal("Spell1","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	GlobalLT("Spell2","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
		IncrementGlobal("Spell2","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	GlobalLT("Spell3","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpellRES("GOLDENCL",NearestEnemyOf(Myself))     //WIZARD_GOLDEN_CLOUD
		IncrementGlobal("Spell3","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_PHANTOM_BLADE)
	HaveSpell(WIZARD_MIRROR_IMAGE)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MIRROR_IMAGE)
		Spell(Myself,WIZARD_PHANTOM_BLADE)
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),30)
END


